/-
Copyright (c) 2021-2023 by the authors listed in the file AUTHORS and their
institutional affiliations. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Harun Khan, Tomaz Mascarenhas
-/

import Mathlib.Analysis.Calculus.Taylor
import Mathlib.Analysis.Convex.SpecificFunctions.Basic
import Mathlib.Analysis.Convex.SpecificFunctions.Deriv
import Mathlib.Analysis.SpecialFunctions.ExpDeriv
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv
import Mathlib.Analysis.Complex.Exponential

import Mathlib.Tactic

open scoped Nat

open Set Real

namespace Smt.Reconstruct.Real

theorem concaveOn_sin_Icc : ConcaveOn â„ (Icc 0 Ï€) sin := StrictConcaveOn.concaveOn strictConcaveOn_sin_Icc

theorem strictConvexOn_sin_Icc : StrictConvexOn â„ (Icc (- Ï€) 0) sin := by
  apply strictConvexOn_of_deriv2_pos (convex_Icc _ _) continuousOn_sin fun x hx => ?_
  rw [interior_Icc] at hx
  simp only [mem_Ioo] at hx
  simp
  have âŸ¨hxâ‚, hxâ‚‚âŸ© := hx
  exact Real.sin_neg_of_neg_of_neg_pi_lt hxâ‚‚ hxâ‚

theorem convexOn_sin_Icc : ConvexOn â„ (Icc (- Ï€) 0) sin := StrictConvexOn.convexOn strictConvexOn_sin_Icc

theorem iteratedDeriv_sin_cos (n : Nat) :
  (iteratedDeriv n sin =
    if n % 4 = 0 then sin else
    if n % 4 = 1 then cos else
    if n % 4 = 2 then -sin else
    -cos) âˆ§
    (iteratedDeriv n cos =
    if n % 4 = 0 then cos else
    if n % 4 = 1 then -sin else
    if n % 4 = 2 then -cos else
    sin) := by
  induction' n with n ih
  Â· simp
  Â· simp [ih.2, iteratedDeriv_succ']
    have :=  Nat.mod_lt n (show 4 > 0 by decide)
    interval_cases hn : n % 4
      <;> simp [hn, Nat.add_mod]
      <;> ext
      <;> have : (fun x => (-sin x)) = -Real.sin := rfl
      <;> simp [this, iteratedDeriv_neg, ih]

theorem iteratedDerivWithin_eq_iteratedDeriv {f : Real â†’ Real} (hf : ContDiff Real (âŠ¤ : â„•âˆ) f) (hs : UniqueDiffOn Real s):
  âˆ€ x âˆˆ s, iteratedDerivWithin d f s x = iteratedDeriv d f x := by
  induction' d with d hd
  Â· simp
  Â· intro x hx
    rw [iteratedDerivWithin_succ, iteratedDeriv_succ, derivWithin, deriv]
    rw [fderivWithin_congr hd (hd x hx)]
    rw [fderivWithin_eq_fderiv (UniqueDiffOn.uniqueDiffWithinAt hs hx)]
    apply Differentiable.differentiableAt (ContDiff.differentiable_iteratedDeriv d hf (Batteries.compareOfLessAndEq_eq_lt.mp rfl))

theorem iteratedDerivWithin_congr {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {f : ğ•œ â†’ F} {fâ‚ : ğ•œ â†’ F} {x : ğ•œ} {s : Set ğ•œ} (hs : Set.EqOn fâ‚ f s) (_hxs : UniqueDiffOn ğ•œ s) (hx2 : x âˆˆ s) : iteratedDerivWithin n fâ‚ s x = iteratedDerivWithin n f s x := by
  revert x
  induction' n with n hn
  <;> intro x hx2
  Â· have hx : fâ‚ x = f x := hs hx2; simp [hx]
  Â· simp only [iteratedDerivWithin_succ]
    simp only [Set.EqOn] at hs
    rw [derivWithin_congr (by simp [Set.EqOn]; intro y hy; exact hn hy) (hn hx2)]

theorem deriv_comp_mul {f : Real â†’ Real} (hd : Differentiable Real f) :
    âˆ€ x, deriv (fun x => f (c*x)) x = c * deriv f (c*x) := by
  intro x
  rw [show (fun x => f (c*x)) = f âˆ˜ (fun x => c*x) by rfl]
  rw [deriv_comp _ (Differentiable.differentiableAt hd) (by apply DifferentiableAt.const_mul (differentiableAt_fun_id))]
  rw [deriv_const_mul _ (differentiableAt_fun_id), mul_comm]
  simp

theorem iteratedDeriv_const_mul {f : â„ â†’ â„ } (d : Nat) (c : Real) (hf : ContDiff Real (âŠ¤ : â„•âˆ) f) :
    âˆ€ x, iteratedDeriv d (fun x => f (c*x)) x = c^d * (iteratedDeriv d f (c*x)) := by
  induction' d with d ih
  Â· simp
  Â· intro x
    rw [iteratedDeriv_succ]
    rw [congrArg deriv (funext ih)]
    rw [deriv_const_mul, deriv_comp_mul, â† iteratedDeriv_succ, pow_succ', â† mul_assoc]
    Â· linarith
    Â· apply ContDiff.differentiable_iteratedDeriv d hf (Batteries.compareOfLessAndEq_eq_lt.mp rfl)
    Â· apply Differentiable.differentiableAt
      rw [show (fun x => iteratedDeriv d f (c * x)) = ((iteratedDeriv d f) âˆ˜ (fun x => c*x)) by rfl]
      apply Differentiable.comp (ContDiff.differentiable_iteratedDeriv d hf (Batteries.compareOfLessAndEq_eq_lt.mp rfl))
      apply Differentiable.const_mul (differentiable_id)

lemma taylorCoeffWithin_neg (d _ : â„•) (hf : ContDiff Real (âŠ¤ : â„•âˆ) f):
    taylorCoeffWithin f d Set.univ xâ‚€ = (-1 : Real)^d * taylorCoeffWithin (fun x => f (-x)) d Set.univ (-xâ‚€) := by
  simp only [taylorCoeffWithin]
  rw [â† iteratedDerivWithin_congr (f := fun i => f (-i)) (fâ‚ := fun i => f (-1*i)) (by simp [Set.eqOn_refl]) uniqueDiffOn_univ (mem_univ _)]
  rw [iteratedDerivWithin_univ, iteratedDerivWithin_univ, iteratedDeriv_const_mul d _ hf]
  simp only [ mul_neg, neg_mul, one_mul, neg_neg]
  rw [mul_comm, smul_eq_mul, smul_eq_mul, mul_assoc]; congr 1
  rw [mul_comm, â† mul_assoc, â† pow_add, â† two_mul, Even.neg_one_pow (by simp), one_mul]

theorem taylorWithinEval_neg {f : Real â†’ Real} (hf : ContDiff Real (âŠ¤ : â„•âˆ) f) :
    taylorWithinEval f n Set.univ xâ‚€ x = taylorWithinEval (fun x => f (-x)) n Set.univ (-xâ‚€) (-x) := by
  unfold taylorWithinEval
  unfold taylorWithin
  rw [Finset.sum_congr rfl
      (by intro d _; rw [taylorCoeffWithin_neg d n hf])]
  simp
  apply Finset.sum_congr rfl
  intro d _
  simp [â† mul_pow, â† mul_assoc]
  apply Or.inl; ring

theorem derivWithin_eq_deriv {f : Real â†’ Real} (hf : ContDiff Real âŠ¤ f) (hs : UniqueDiffWithinAt Real s x) :
    derivWithin f s x = deriv f x := by
  simp only [derivWithin, deriv]
  rw [fderivWithin_eq_fderiv hs (ContDiffAt.differentiableAt (ContDiff.contDiffAt hf) (by simp))]

-- This should be generalized
theorem taylorCoeffWithin_eq {f : Real â†’ Real} (s : Set Real) (hx : xâ‚€ âˆˆ s) (hs : UniqueDiffOn â„ s) (hf : ContDiff Real (âŠ¤ : â„•âˆ) f):
    (taylorCoeffWithin f d s xâ‚€) = (taylorCoeffWithin f d Set.univ xâ‚€) := by
  simp only [taylorCoeffWithin]
  rw [iteratedDerivWithin_eq_iteratedDeriv hf hs _ hx, iteratedDerivWithin_univ]

-- This should be generalized
theorem taylorWithinEval_eq {f : Real â†’ Real} (s : Set Real) (hs : xâ‚€ âˆˆ s) (hs1 : UniqueDiffOn â„ s) (hf : ContDiff Real (âŠ¤ : â„•âˆ) f) :
    (taylorWithinEval f d s xâ‚€) = (taylorWithinEval f d Set.univ xâ‚€) := by
  ext x
  simp only [taylorWithinEval, taylorWithin, taylorCoeffWithin_eq s hs hs1 hf]

theorem taylor_mean_remainder_lagrangeâ‚ {f : â„ â†’ â„} {x xâ‚€ : â„} (n : â„•) (hx : x < xâ‚€)
    (hf : ContDiff â„ (âŠ¤ : â„•âˆ) f) :
    âˆƒ x' âˆˆ Ioo x xâ‚€, f x - taylorWithinEval f n (Icc x xâ‚€) xâ‚€ x =
      iteratedDerivWithin (n + 1) f (Icc x xâ‚€) x' * (x - xâ‚€) ^ (n + 1) / (n + 1)! := by
  have H1 : ContDiff â„ (âŠ¤ : â„•âˆ) fun p => f (-p) := (show (f âˆ˜ (fun x => -x)) = (fun p => f (-p)) by rfl) â–¸ ContDiff.comp hf contDiff_neg
  have H2 : DifferentiableOn â„ (iteratedDerivWithin n (fun x => f (-x)) (Icc (-xâ‚€) (-x))) (Ioo (-xâ‚€) (-x)) := by
    apply DifferentiableOn.mono _ Set.Ioo_subset_Icc_self
    apply ContDiffOn.differentiableOn_iteratedDerivWithin (n := n + 1) _ (by norm_cast; simp) (uniqueDiffOn_Icc (neg_lt_neg hx))
    apply ContDiff.contDiffOn ((contDiff_infty.mp H1) (n + 1))
  have âŸ¨x' , hx', HâŸ©:= taylor_mean_remainder_lagrange (f := fun x => f (-x)) (n := n) (neg_lt_neg hx) (ContDiff.contDiffOn ((contDiff_infty.mp H1) n)) H2
  have hx'' : -x' âˆˆ Ioo x xâ‚€ := by
    simp at *
    apply And.intro
    <;> apply neg_lt_neg_iff.mp
    <;> simp [hx']
  use -x', hx''
  rw [neg_neg, taylorWithinEval_eq _ (by simp [le_of_lt hx]) (uniqueDiffOn_Icc (by simp [hx])) H1] at H
  rw [taylorWithinEval_neg H1, â†taylorWithinEval_eq (Icc x xâ‚€) (by simp [le_of_lt hx]) (uniqueDiffOn_Icc hx) (by simp [hf])] at H
  simp only [neg_neg, sub_neg_eq_add] at H
  rw [H, iteratedDerivWithin_eq_iteratedDeriv hf (uniqueDiffOn_Icc (by simp [hx])) _ (Set.Ioo_subset_Icc_self hx''), iteratedDerivWithin_eq_iteratedDeriv H1 (uniqueDiffOn_Icc (by simp [hx])) _ (Set.Ioo_subset_Icc_self hx')]
  rw [show (fun x => f (-x)) = (fun x => f (-1 * x)) by simp]
  rw [iteratedDeriv_const_mul _ _ hf, mul_rotate, mul_assoc, â†mul_pow, add_comm (-x) xâ‚€]
  simp [sub_eq_add_neg]

theorem iteratedDerivWithin_sin_eq_zero_of_even (j : â„•) (hj : Even j) :
  iteratedDerivWithin j sin univ 0 = 0 := by
  have := Nat.mod_lt j (show 4 > 0 by decide)
  interval_cases h : j % 4
  <;> rw [â† Even.mod_even_iff (show Even 4 by decide), h] at hj
  <;> try {simp only [show Â¬ Even 3 by decide, Nat.not_even_one] at hj}
  <;> rw [iteratedDerivWithin_eq_iteratedDeriv contDiff_sin uniqueDiffOn_univ 0 (Set.mem_univ 0)]
  <;> simp [iteratedDeriv_sin_cos, h]

theorem taylorSin_neg (x : Real) (d : Nat) :
  let p: â„ â†’ â„ := taylorWithinEval Real.sin d Set.univ 0
  p (-x) = -p x := by
  intro p
  simp only [p, taylor_within_apply, sub_zero]
  rw [â† Finset.sum_neg_distrib]
  apply Finset.sum_congr rfl
  intro j hj
  cases' (Nat.even_or_odd j) with h h
  Â· rw [iteratedDerivWithin_sin_eq_zero_of_even j h]
    simp
  Â· rw [Odd.neg_pow h]
    simp

theorem neg_one_le_iteratedDeriv_sin (n : Nat) (x : Real) : -1 â‰¤ (iteratedDeriv n sin) x := by
  have :=  Nat.mod_lt n (show 4 > 0 by decide)
  interval_cases hn : n % 4
  <;> simp [iteratedDeriv_sin_cos, hn, sin_le_one, neg_one_le_sin, cos_le_one, neg_one_le_cos]

theorem iteratedDeriv_sin_le_one (n : Nat) (x : Real) : (iteratedDeriv n sin) x â‰¤ 1 := by
  have :=  Nat.mod_lt n (show 4 > 0 by decide)
  interval_cases hn : n % 4
  <;> simp [iteratedDeriv_sin_cos, hn, sin_le_one, neg_one_le_sin, cos_le_one, neg_one_le_cos, neg_le]

theorem neg_one_le_iteratedDeriv_cos (n : Nat) (x : Real) : -1 â‰¤ (iteratedDeriv n cos) x := by
  have :=  Nat.mod_lt n (show 4 > 0 by decide)
  interval_cases hn : n % 4
  <;> simp [iteratedDeriv_sin_cos, hn, sin_le_one, neg_one_le_sin, cos_le_one, neg_one_le_cos]

theorem iteratedDeriv_cos_le_one (n : Nat) (x : Real) : (iteratedDeriv n cos) x â‰¤ 1 := by
  have :=  Nat.mod_lt n (show 4 > 0 by decide)
  interval_cases hn : n % 4
  <;> simp [iteratedDeriv_sin_cos, hn, sin_le_one, neg_one_le_sin, cos_le_one, neg_one_le_cos, neg_le]

end Smt.Reconstruct.Real
